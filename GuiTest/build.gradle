plugins {
	id 'fabric-loom'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

if(rootProject.file('private.gradle').exists()) { //Publishing details
	apply from: rootProject.file('private.gradle')
}

archivesBaseName = "LibGuiTest"
version = rootProject.version
group = rootProject.group

loom {
	runConfigs.configureEach {
		ideConfigGenerated = true
	}
}

//configurations.modApi {
//    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//}

repositories {
	mavenLocal();
	maven { url "http://server.bbkr.space:8081/artifactory/libs-release" }
}

dependencies {
	minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
	mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
	modApi "net.fabricmc:fabric-loader:${rootProject.loader_version}"
	modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"

	compileOnly ("com.google.code.findbugs:jsr305:3.0.2") { transitive = false }

	implementation project(':')
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile) {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	options.encoding = "UTF-8"

	if (JavaVersion.current().isJava9Compatible()) {
		options.release.set 8
	}
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}
